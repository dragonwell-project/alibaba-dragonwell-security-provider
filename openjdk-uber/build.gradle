description = 'Dragonwell Security: OpenJdk UberJAR'

ext {
    buildUberJar = Boolean.parseBoolean(System.getProperty('com.alibaba.dragonwell.security.native.buildUberJar', 'false'))
    uberJarClassifiers = (System.getProperty('com.alibaba.dragonwell.security.native.uberJarClassifiers',
            'osx-x86_64,osx-aarch_64,linux-x86_64,linux-aarch_64')).split(',')
    classesDir = "${buildDir}/classes"
    resourcesDir = "${buildDir}/resources"
    sourcesDir = "${buildDir}/sources"
}

if (buildUberJar) {
    apply plugin: 'biz.aQute.bnd.builder'

    configurations {
        uberJar
    }

    // Point the jar task to the copied classes and resources directories.
    jar {
        from classesDir
        from resourcesDir
    }

    sourcesJar {
        from sourcesDir
    }

    // Add the dependencies for the uber jar.
    uberJarClassifiers.each { uberJarClassifier ->
        dependencies.uberJar "${group}:security-native:${version}:${uberJarClassifier}"
    }

    /**
     * Copy the native libraries to the resources directory.
     */
    def copySharedLibs = tasks.register("copySharedLibs",  Copy) {
        dependsOn configurations.uberJar
        from {
            configurations.uberJar.collect {
                zipTree(it)
            }
        }
        include '/META-INF/native/**'
        into file(resourcesDir)
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    tasks.named("jar").configure {
        dependsOn copySharedLibs
    }

    /**
     * Copy the object files to the classes directory.
     */
    def copyClasses = tasks.register("copyClasses", Copy) {
        dependsOn configurations.uberJar
        from {
            configurations.uberJar.collect {
                zipTree(it)
            }
        }
        exclude '/META-INF/**'
        into file(classesDir)
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    tasks.named("jar").configure {
        dependsOn copyClasses
    }

    def copySources = tasks.register("copySources", Copy) {
        dependsOn ":security-native:sourcesJar"
        from {
            project(":security-native").sourceSets.main.java
        }
        into file(sourcesDir)
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    tasks.named("sourcesJar").configure {
        dependsOn copySources
    }

    // Note that this assumes that the version of BoringSSL for each
    // artifact exactly matches the one on the current system.
    jar.manifest {
        attributes ('TONGSUO-Version': tongsuoVersion,
                    'Automatic-Module-Name': 'com.alibaba.dragonwell.security, org.conscrypt',
                    'Bundle-SymbolicName': 'com.alibaba.dragonwell.security',
                    '-exportcontents': 'com.alibaba.dragonwell.security.*, org.conscrypt.*')
    }

    apply from: "$rootDir/gradle/publishing.gradle"
    publishing.publications.maven {
        artifact sourcesJar
        artifact javadocJar
        artifact jar
    }
} else {
    // Not building an uber jar - disable all tasks.
    tasks.configureEach {
        it.enabled = false
    }
}
