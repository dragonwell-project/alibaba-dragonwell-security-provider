apply plugin: 'maven-publish'
apply plugin: 'signing'

def isSnapshot = project.version.contains('SNAPSHOT')

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                afterEvaluate {
                    name = "$project.group:$project.name" as String
                    description = project.description
                }

                url = 'https://www.tongsuo.net/'

                scm {
                    connection = 'scm:git:https://github.com/Tongsuo-Project/tongsuo-java-sdk.git'
                    developerConnection = 'scm:git:git@github.com:Tongsuo-Project/tongsuo-java-sdk.git'
                    url = 'https://github.com/Tongsuo-Project/tongsuo-java-sdk'
                }

                licenses {
                    license {
                        name = 'Apache 2'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id = 'tongsuo'
                        name = 'Tongsuo Project Authors'
                        email = 'zhangchenglong@tongsuo.net'
                        url = 'https://www.tongsuo.net/'
                        organization = 'Tongsuo Project'
                        organizationUrl = 'https://github.com/Tongsuo-Project'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            def stagingUrl = rootProject.hasProperty('repositoryId') ? \
                'https://s01.oss.sonatype.org/service/local/staging/deployByRepositoryId/' \
                    + rootProject.repositoryId : \
                'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            url isSnapshot ? snapshotUrl : stagingUrl
            credentials {
                username = rootProject.findProperty('ossrhUsername') ?: ''
                password = rootProject.findProperty('ossrhPassword') ?: ''
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

signMavenPublication.doFirst {
    publishing.publications.maven.artifacts.each {
        if (it.file.absolutePath.endsWith('.jar') && it.classifier != 'sources' && it.classifier != 'javadoc') {
            logger.info("Signing jar: ${it.file.absolutePath}")
            signJar(it.file.absolutePath)
        }
   }
}
